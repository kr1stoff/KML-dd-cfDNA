# 加载配置文件
configfile: workflow.source_path("config.yaml")


from snakemake.utils import validate

# 验证配置文件
validate(config, "config.schema.yaml")

# 定义 shell 环境
shell.executable("/bin/bash")
shell.prefix("set +eu;")


# 定义最终输出结果
rule all:
    input:
        "fastp/fq_summary.tsv",
        "multiqc"


# 导入公共规则
include: "rules/common.smk"


# QC
rule fastqc:
    input:
        rules.create_symlinks.output.fq1,
        rules.create_symlinks.output.fq2,
    output:
        directory("fastqc/{sample}"),
    benchmark:
        ".log/fastqc/{sample}.bm"
    log:
        ".log/fastqc/{sample}.log",
    conda:
        config["conda"]["fastqc"]
    threads: config["threads"]["low"]
    shell:
        "mkdir {output} && fastqc {input} -o {output} -t {threads} --extract &> {log}"


rule multiqc:
    input:
        expand("fastqc/{sample}", sample=samples),
    output:
        directory("multiqc"),
    benchmark:
        ".log/multiqc/multiqc.bm"
    log:
        ".log/multiqc/multiqc.log",
    conda:
        config["conda"]["multiqc"]
    shell:
        "multiqc {input} --outdir {output} 2> {log}"


# TODO 参考艾吉泰康 UMI 处理


rule fastp:
    input:
        sample=[
            rules.create_symlinks.output.fq1,
            rules.create_symlinks.output.fq2,
        ],
    output:
        trimmed=["fastp/{sample}.1.fastq.gz", "fastp/{sample}.2.fastq.gz"],
        html="fastp/{sample}.html",
        json="fastp/{sample}.json",
    log:
        ".log/fastp/{sample}.fastp.log",
    benchmark:
        ".log/fastp/{sample}.fastp.bm"
    conda:
        config["conda"]["fastp"]
    threads: config["threads"]["low"]
    params:
        extra="-q 15 -u 40 -l 25 --cut_right --cut_window_size 20 --cut_mean_quality 30 --correction",
    shell:
        """
        fastp -w {threads} {params.extra} \
            -i {input.sample[0]} -I {input.sample[1]} \
            -o {output.trimmed[0]} -O {output.trimmed[1]} \
            -h {output.html} -j {output.json} 2> {log}
        """


rule fq_stats_summary:
    input:
        expand("fastp/{sample}.json", sample=samples),
    output:
        "fastp/fq_summary.tsv",
    benchmark:
        ".log/fastp/fq_all_samples_qc.bm"
    log:
        ".log/fastp/fq_all_samples_qc.log",
    conda:
        config["conda"]["python"]
    script:
        "scripts/fq_all_samples_qc.py"


